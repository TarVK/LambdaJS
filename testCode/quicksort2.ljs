// Natural number constructors, peano numbers (Zero and Successor) and functions
Z;
S nat;

lessThan Z (S x) = True;
lessThan x Z = False;
lessThan (S x) (S y) = lessThan x y;

// Boolean constructors
False;
True;

iff True x y = x;
iff False x y = y; 

// List constructors (linked list of sorts)
Cons head tail;
Nill;

concat Nill list = list;
concat (Cons head tail) list = Cons head (concat tail list);

// Pair constructor
Pair el1 el2;

// A function to append a value to either the left or right list, given a Pair of lists
appendFirst val (Pair list1 list2) = Pair (Cons val list1) list2;
appendSecond val (Pair list1 list2) = Pair list1 (Cons val list2);  

// Quicksort (except it's not quick at all when compiled)
partition val Nill = Pair Nill Nill;
partition val (Cons head tail) 
    = (iff (lessThan head val) appendFirst appendSecond) head (partition val tail);
    
quicksort Nill = Nill;
quicksort (Cons head tail) = quicksort head (partition head tail);

quicksort val (Pair lessThan moreThan) 
    = concat 
        (quicksort lessThan) 
        (Cons val (quicksort moreThan)); 

// Perform quicksort on some sample data
v0 = Z;
v1 = S v0;
v2 = S v1;
v3 = S v2;
v4 = S v3;
v5 = S v4;
v6 = S v5;
v7 = S v6;
v8 = S v7;
list = Cons v7 (Cons v0 (Cons v3 (Cons v4 (Cons v1 (Cons v0 Nill)))));

output quicksort list;